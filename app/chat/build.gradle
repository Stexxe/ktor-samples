buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin-multiplatform'

kotlin {
    targets {
        js('frontend') {
            browser {
                testTask {
                    enabled = false
                }

                distribution {
                    directory = file("$projectDir/src/backendMain/resources/web")
                }
            }
        }
        jvm('backend') {
            compilations.main {
                tasks.getByName(processResourcesTaskName) {
                    dependsOn(frontendBrowserWebpack)
                }
            }
        }
    }
}

repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}

dependencies {
    backendMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    backendMainImplementation "io.ktor:ktor-server-netty:$ktor_version"
    backendMainImplementation "io.ktor:ktor-websockets:$ktor_version"
    backendMainImplementation "ch.qos.logback:logback-classic:$logback_version"

    backendTestImplementation "io.ktor:ktor-server-test-host:$ktor_version"

    frontendMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-js"
}

task run(type: JavaExec, dependsOn: [backendJar]) {
    main = "io.ktor.samples.chat.backend.ChatApplicationKt"
    classpath(configurations.backendRuntimeClasspath, backendJar)
    args = []
}
